load("@bazel_skylib//rules:build_test.bzl", "build_test")
load(
    "//updatesrc/internal:updatesrc_update_test.bzl",
    "updatesrc_update_test",
)
load(
    "//updatesrc/internal:updatesrc_update.bzl",
    "updatesrc_update",
)

build_test(
    name = "public_api",
    targets = [
        "//updatesrc:deps",
        "//updatesrc:updatesrc",
    ],
)

_LETTERS = [
    "a",
    "b",
    "c",
]

_LETTER_SRCS = [letter + ".txt" for letter in _LETTERS]

_LETTER_OUTS = [":" + letter + "_modified" for letter in _LETTERS]

[
    genrule(
        name = letter + "_src",
        srcs = [],
        outs = [letter + ".txt"],
        cmd = """\
    echo "This is {letter}.txt." > $@
    """.format(letter = letter),
    )
    for letter in _LETTERS
]

[
    genrule(
        name = letter + "_modified",
        srcs = [letter + ".txt"],
        outs = [letter + ".txt_modified"],
        cmd = """\
    echo "# Howdy" > $@
    cat $(location {letter}.txt) >> $@
    """.format(letter = letter),
    )
    for letter in _LETTERS
]

updatesrc_update(
    name = "src_out",
    srcs = _LETTER_SRCS,
    outs = _LETTER_OUTS,
)

updatesrc_update_test(
    name = "src_out_test",
    srcs = _LETTER_SRCS,
    outs = _LETTER_OUTS,
    update_target = ":src_out",
)
